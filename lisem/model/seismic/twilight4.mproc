with(`codegen`);

# New WPP forcing
u      := sin(om*(x-c*t))*sin(om*y+ph)*sin(om*z+ph);
v      := sin(om*x+ph)*sin(om*(y-c*t))*sin(om*z+ph);
w      := sin(om*x+ph)*sin(om*y+ph)*sin(om*(z-c*t));
rho    := amprho*(2 + sin(omm*x+phm)*cos(omm*y+phm)*sin(omm*z+phm) );
mu     := ampmu*(3 + cos(omm*x+phm)*sin(omm*y+phm)*sin(omm*z+phm) );
lambda := amplambda*(2 + sin(omm*x+phm)*sin(omm*y+phm)*cos(omm*z+phm) );

# twilight forcing for the elastic wave equation
#
# NOTE: rho multiplying 2nd time derivative!!!!
#
fu    := rho*diff(diff(u,t),t) - (diff( (2*mu+lambda)*diff(u,x)+ lambda*diff(v,y)+lambda*diff(w,z),x) + diff( mu*(diff(v,x)+diff(u,y)),y) + diff( mu*(diff(w,x)+diff(u,z)),z) );
fv    := rho*diff(diff(v,t),t) - (diff( mu*(diff(v,x)+diff(u,y)),x) + diff( (2*mu+lambda)*diff(v,y)+lambda*(diff(u,x)+diff(w,z)),y) + diff( mu*(diff(w,y)+diff(v,z)),z) );
fw    := rho*diff(diff(w,t),t) - (diff( mu*(diff(u,z)+diff(w,x)),x) + diff( mu*(diff(v,z)+diff(w,y)),y) + diff( (2*mu+lambda)*diff(w,z)+lambda*(diff(u,x)+diff(v,y)),z) );

# only right hand side
#fu    := diff( (2*mu+lambda)*diff(u,x)+ lambda*diff(v,y)+lambda*diff(w,z),x)+diff( mu*(diff(v,x)+diff(u,y)),y)+diff( mu*(diff(w,x)+diff(u,z)),z);
#fv    := diff( mu*(diff(v,x)+diff(u,y)),x) + diff( (2*mu+lambda)*diff(v,y)+lambda*(diff(u,x)+diff(w,z)),y) + diff( mu*(diff(w,y)+diff(v,z)),z);
#fw    := diff( mu*(diff(u,z)+diff(w,x)),x) + diff( mu*(diff(v,z)+diff(w,y)),y) + diff( (2*mu+lambda)*diff(w,z)+lambda*(diff(u,x)+diff(v,y)),z);

forces := vector([fu,fv,fw]);
fg := makeproc(forces,parameters=[x,y,z,t,om,c,ph,omm,phm,amprho,ampmu,amplambda]);
fortran(fg,optimized);

futt := diff(diff(fu,t),t);
fvtt := diff(diff(fv,t),t);
fwtt := diff(diff(fw,t),t);

forces := vector([futt,fvtt,fwtt]);
fgtt := makeproc(forces,parameters=[x,y,z,t,om,c,ph,omm,phm,amprho,ampmu,amplambda]);
fortran(fgtt,optimized);

#fsurf1 := mu*(diff(u,z)+diff(w,x));
#fsurf2 := mu*(diff(v,z)+diff(w,y));
#fsurf3 := 2*mu*diff(w,z)+lambda*(diff(u,x)+diff(v,y)+diff(w,z));
#forces := vector([fsurf1, fsurf2, fsurf3]);
#ffsurfz := makeproc(forces,parameters=[x,y,z,t,om,c,ph,omm,phm,amprho,ampmu,amplambda]);
#fortran(ffsurfz,optimized);

